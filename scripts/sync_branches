#!/bin/zsh

# This script automates the synchronization of multiple Git branches. It defines 
# a set of parent branches and their corresponding child branches, then iterates 
# through each parent-child relationship to ensure that changes in the parent 
# branches are propagated to their child branches. This helps maintain 
# consistency across the branches by rebasing and force-pushing updates.

set -euo pipefail

typeset -A BRANCHES

BRANCHES=(
  [master]="unix windows"
  [unix]="linux darwin"
)


TRAPZERR() {
  local err_code=$?
  local abs_file_path="${${funcfiletrace[1]}%%:*}"
  local abs_lineno="${${funcfiletrace[1]}##*:}"

  echo "Error (code $err_code) thrown in $abs_file_path at line $abs_lineno." \
  > /dev/tty
}

{ gsp; gpu }

sync_and_rebase_branches() {
  local parent_branch=$1
  local child_branch=$2

  if [[ $parent_branch == "master" ]]; then
    git switch $parent_branch
    git pull origin $parent_branch
  fi

  git switch $child_branch
  git pull origin $child_branch
  git rebase $parent_branch
  git push --force-with-lease origin $child_branch
}

for parent_branch in ${(k)BRANCHES}; do
  for child_branch in ${(z)BRANCHES[$parent_branch]}; do
    [[ $child_branch == ${$(uname -s):l} ]] && PLATFORM=$child_branch
    sync_and_rebase_branches $parent_branch $child_branch &> /dev/null
  done
done

echo "Everything is up to date."

if [[ ${PLATFORM:-} != $(git branch --show-current) ]]; then
  git switch -q $PLATFORM
fi
